; position, texture, 2 colors rgb

; Uniforms
.fvec projection[4], modelView[4]
.fvec tex_scale

; Constants
.constf myconst(0.0, 1.0, -1.0, -0.5)
.constf tcofs(0.0, 1.0, 0.0, 0.0)

.alias  zeros myconst.xxxx ; Vector full of zeros
.alias  ones  myconst.yyyy ; Vector full of ones

; Inputs
.in inpos v0
.in intex v1
.in inclr v2; ignore first color for now...
.in inclr2 v3

; Outputs
.out outpos position  ; xyzw
.out outtc0 texcoord0 ; texture unit 0
.out outtc1 texcoord1 ; texture unit 1
.out outclr color     ; rgba

.proc main

setup:
    mov r0, inpos
model:
    ; r1 = modelView * inpos
    dp4 r1.x, modelView[0], r0
    dp4 r1.y, modelView[1], r0
    dp4 r1.z, modelView[2], r0
    dp4 r1.w, modelView[3], r0
projection:
    ; outpos = projection * inpos
    dp4 outpos.x, projection[0], r1
    dp4 outpos.y, projection[1], r1
    dp4 outpos.z, projection[2], r1
    dp4 outpos.w, projection[3], r1
texture:
    ; outtex = intex
    mul r2, tex_scale, intex
    add outtc0, tcofs, r2
    mov outtc1, outtc0
color:
    ; copy color
    mov outclr, inclr2

    ; We're finished
    end
.end
